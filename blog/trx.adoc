[[chapter_trx]]
TRX: parsing in Opa
-------------------

This article is not exactly a continuation of our <<chapter_image_intro, iMage walk-through>>. Not exactly. But quite. In this post I'll introduce you to TRX (please don't ask me about the etymology of this name) -- Opa's extension for _parsing_. In the next post we will put this knowledge to good use by writing a parser for a simple mark-up and using it in iMage for configuration files with images to be shown and their captions.

[icons=None, caption="Summary"]
[NOTE]
=======================
You will learn:

* About http://en.wikipedia.org/wiki/Parsing_expression_grammar[Parsing Expressions Grammars (PEGs)]
* How to handle string parsing in Opa (XML parsing we'll leave for another time)
=======================

Web apps and parsing
~~~~~~~~~~~~~~~~~~~~

Parsing is something that web apps need to do quite often, to process certain inputs. For instance think about writing a wiki. Chances are you will want to use some (non-HTML) mark-up for its pages. That means you will need to transform this mark-up to the corresponding HTML. That in turns means you will need to parse the mark-up first. Also processing user input via forms usually involves parsing in one form or another.

Fear not, Opa has a great support for writing recursive descent style parsers. Let's first explore the underlying formalism and then we will talk about Opa's support for it.

Hello, PEGs
~~~~~~~~~~~

Opa's parsing is based on http://en.wikipedia.org/wiki/Parsing_expression_grammar[Parsing Expressions Grammars (PEGs)]. It's a relatively new hence not so widely known format, so chances are you haven't heard of it yet. On the other hand you probably heard about http://en.wikipedia.org/wiki/Context-free_grammar[Context Free Grammars (CFGs)]. If that's the case let me quickly sketch main practical differences between CFGs and PEGs, if you don't know CFGs, you can safely skip the following paragraph and jump right into the introduction to PEGs. Also, if you like reading academic papers, you may want to read http://www.lmcs-online.org/ojs/viewarticle.php?id=708&layout=abstract[``TRX: A Formally Verified Parser Interpreter''], where both the theory behind TRX, as well as some efforts towards its formal verification are described. If you prefer more down to earth stuff -- read on :).

The main difference between PEGs and CFGs is that the former ones are always unambiguous. And that's a good thing (at least if you're not into processing natural languages and stuff). Another good thing is that there is a very natural translation from PEGs to http://en.wikipedia.org/wiki/Recursive_descent_parsers[recursive descent parsers]. On the down side parsing PEGs can be expensive (read: exponential). One way to address that is to use memoization which makes parsing (worst-case) complexity linear at the price of also linear memory consumption. For now the built-in parsing mechanisms in Opa do not use memoization so we need to be careful when writing parsers... At least that's the theory, in practice you'll probably never encounter such problems, unless you're writing serious grammars like, say, parsing some programming language.

... and hello TRX
~~~~~~~~~~~~~~~~~

You know what, I know I said I'll first introduce PEGs and then talk about TRX, but after all this blog is supposed to be practical. So let's talk about TRX explaining the necessary minimum of PEGs as we go along. If you want to learn a bit more about the theoretical part of TRX then read the http://www.lmcs-online.org/ojs/viewarticle.php?id=708&layout=abstract[aforementioned article] (or one of the many articles about PEGs themselves). You may also want to read about PEGs on http://en.wikipedia.org/wiki/Parsing_expression_grammar[Wikipedia].


Let's start right away with our first Opa parser:

[source, opa]
------------------------
our_first_parser =
  parser "Hello TRX" -> void
------------------------

So let's see what happens here one piece at a time. +our_first_parser+ is just a usual name of a declared value. +parser+ is an Opa keyword and it means that what follows will be a parsing expression. Such an expression has a form +exp -> res+ where +exp+ is the parsing expression to match and +res+ is the result for parsing such an expression -- I'll often say that the parser ``returns'' this value. For +our_first_parser+ the expression is just a literal string +"Hello TRX"+. That means that this parser will succeed only if fed with exactly that string (and will fail otherwise). If the parser succeeds it will produce the result; in this case there's nothing interesting happening so we just return a non-value +void+.

How do we use such a parser? You can take a look at the http://opalang.org/resources/doc/index.html#parser.opa.html/!/[+Parser+] module for a bunch of interesting functions. The most important one is:

[source, opa]
------------------------
Parser.try_parse : Parser.general_parser('a), string -> option('a)
------------------------

+Parser.try_parse+ takes two arguments: a parser of type β to use and an input string and it produces an optional β result, in case parsing succeeds. Woow, ok, I can hear some of you screaming for me to slow down. I hear ya :).

First of all, don't let those Greek letters intimidate you :). (If you're a functional programming guru then skip the few following paragraphs before you start yawning.) The +\'a+ in +Parser.general_parser(\'a)+ is a so-called ``type-variable''. In source code we use identifiers starting with +'+ to denote type-variables. In normal texts it's usual to use Greek letters for them; hence the β above.

Now those type variables are variables standing for types. They are used for polymorphism. For instance what do you think is the type of the following function:

[source, opa]
------------------------
identity(x) = x
------------------------

It's an identity function: it takes an argument and returns it unchanged. It doesn't really matter what is the type of +x+. Therefore the type of this function is:

[source, opa]
------------------------
identity : 'a -> 'a
------------------------

which you can read as: give me an argument of some type +'a+ and I'll return you a value of the same type, i.e. +'a+. So now you can use this function for +integers+ but also for +strings+ without loosing type-safety.

Coming back to parsing. Now we can properly read this type:

[source, opa]
------------------------
Parser.try_parse : Parser.general_parser('a), string -> option('a)
------------------------

What it says is: the function +Parser.try_parse+ takes as an argument a parser (+Parser.general_parser+) for some arbitrary type +'a+ and an input string and returns an optional value of type +'a+. +Parser.general_parser+ is a +parametrized+ type. This is a bit like normal functions (which take values as arguments and return another value) but on the type-level (those _type schemas_ take types as arguments and produce another type as a result).

Now I should probably properly explain what an +option+ type is but that would start a discussion on sum-types in Opa and it's just too much out of scope for this article. We'll come to that. For now you just need to know that the +option(t)+ value is either +none+ (indicating no value) or +some(x)+ with some value +x+ of type +t+. This is btw. a type-safe way to deal with possibly non-existing values (languages like Java or C use +null+ for that).

So let's see how this parsing business works (+-->+ is *not* Opa syntax; I just use it here to indicate the result of evaluating an expression)

[source, opa]
------------------------
Parser.try_parse(our_first_parser, "Hello TRX") --> some(void)
Parser.try_parse(our_first_parser, "Hello, TRX") --> none
------------------------

If we feed the +"Hello TRX"+ string to our parser then parsing succeeds (with +void+) value. If we feed it anything else it fails with +none+.

Ok, now let's see some more interesting ways to construct our parsers. We can put together two parsers in sequence: +A B+ means first parse with +A+ and then with +B+ on the remaining input. So the above parser could as well be written:

[source, opa]
------------------------
our_first_parser_strikes_again = parser "Hello " "TRX" -> void
------------------------

Then there is the choice: +A | B+ means try to parse with +A+ and *if that fails* try to parse with +B+.

[source, opa]
------------------------
choice_parser = parser
| "Hello TRX" -> true
| "Hello world" -> false
------------------------

Notice that we return a boolean value here, so the type of the +choice_parser+ is +Parser.general_parser(bool)+. There is one very important point here: the second (and third etc. as we can have a choice between more than two expressions) branch will be tried *only* if the first one fails.

This can get confusing especially if you're familiar with CFGs as there the choice operator is completely symmetrical. In PEGs the choice is *prioritized*: earlier branches of the choice take priority over the later ones. To emphasize this asymmetry it is usual to denote the PEG choice as +A / B+. However, in Opa we keep +A | B+ syntax as it very nicely mimics the syntax of pattern matching (we'll talk more about pattern matching soon) -- and there are many parallels as the pattern matching is also ``prioritized''. Ok, enough theory.

What else do we have. A dot (+.+) stands for an arbitrary character. A range +[abc]+ stands for any of the characters (so +a+, +b+ or +c+). It's also possible to put ranges there so for instance: +[a-zA-Z]+ means any letter (small or capital).

Finally there is the repetition +A*+ means zero-or-more repetitions of +A+ and +A++ means one-or-more repetitions of +A+. Another important note and another difference with CFGs: those repetitions operators are *greedy*, in the sense that we will process as many copies of +A+ as possible from the input and we will not consider taking less copies than that.

Example! Consider: +(a|b)* b+. In CFGs that would mean a string composed of +a+ and +b+ letters with +b+ at the end. In PEGs such a parser will never succeed. Why? Consider the string +ab+. The repetition operator is *greedy*, so it will ``eat'' as much as it can. That means the whole +ab+. So there is no input left to match the +b+ at the end.

Ok, I can see you folks getting confused (unless there are some PEG ninjas hiding among you) so let's hit a couple of examples.

[source, opa]
------------------------
anything = .* -> void
------------------------

Dot is any character, so +.*+ means a sequence of arbitrary characters. That means this parser will succeed on any input and consume it completely.

[source, opa]
------------------------
digits = parser [0-9]+ -> void
------------------------

The +digits+ parser accept a (non-empty) sequence of digits. For now we only saw +void+ parsers, but for this one we would actually like to return the natural number corresponding to the given sequence of digits. To do that we can give names to parsed entries as follows:

[source, opa]
------------------------
digits2 = parser res=[0-9]+ -> res
------------------------

So +res+ is the result of parsing the +[0-9]++ expression and that's what this parser will return. Now, the result of parsing a repetition is the list of parsed elements. The result of parsing a range, +[0-9]+, is a parsed character. So +digits2+ parser will return a list of characters.

However, we would like to return the integer. For that we can use the +Int.of_string+ function but first we need to get hold of the parsed string to feed to it. In order to do that we can just put the expression in parentheses. A parsing expression +(A -> res)+ will return +res+ and when that is not given explicitly, as in +(A)+ it just gives the string being the part of the input that was matched with +A+.

\... or, I should say, almost a string. There's a little caveat here. For efficiency reasons TRX does not use the +string+ data-type but +text+, which is an Opa's version of a http://en.wikipedia.org/wiki/Rope_(computer_science)[rope] data-type. To get string out of a +t: text+ we can use explicit conversion +Text.to_string(t)+, or use a string insert +"{t}\"+, which will make an implicit conversion from rope to string. Yeah, I know, a bit annoying. We're trying to unify +string+ and +text+ (without sacrificing efficiency), but before we can do that, bear with us.

So what we end up with is a parser for natural numbers:

[source, opa]
------------------------
natural = parser res=([0-9]+) -> Int.of_string("{res}")
------------------------

Another useful way to construct parsers is +A?+, which indicates an optional occurrence of +A+ and gives +some(v)+ (where +v+ was returned by parsing with +A+) if +A+ was matched and +none+ otherwise.

That means we can write a parser for integers as follows:

[source, opa]
------------------------
natural = parser sign=[-]? v=natural -> Option.switch(_ -> -v, v, sign)
------------------------

+Option.switch(f_some, v_none, opt)+ returns +f_some(v)+ it +opt=some(v)+ and returns +v_none+ if +opt=none+. So if the minus sign is present we will return +-v+, otherwise +v+. Another interesting thing here is that we refer to the previously defined parser +natural+, which is perfectly admissible.

In fact we can even call functions that will return parsers, although in this case they need to be written as +{...}+ inserts. For instance let's take a look at the function http://opalang.org/resources/doc/index.html#rules.opa.html/!/value_stdlib.core.parser.Rule.rep[+Rule.rep+] from the standard library (or a small variant of it).

[source, opa]
------------------------
rep(n : int, rule : Parser.general_parser('a)) : Parser.general_parser(list('a)) =
  if n > 0 then
    parser x=rule xs={rep(n - 1, rule)} -> [x | xs]
  else
    parser Rule.succeed -> []
------------------------

Let's start with the header:

[source, opa]
------------------------
rep(n : int, rule : Parser.general_parser('a)) : Parser.general_parser(list('a))
------------------------

This means that the +rep+ function takes two arguments: an integer +n+ and a parser +rule+ returning arbitrary type +'a+. The +rep+ function itself will return a parser returning a list of +'a+ values, +list('a)+.

Now, the body. The idea is that +rep(n, rule)+ is a parser that parses *exactly* +n+ copies of +rule+ (if we didn't care about the exact number we could just use +rule++ or +rule*+). If +n > 0+ then we should parse a copy of +rule+, obtaining result +x+, and then parse another +n-1+ copies of it, obtaining a list of results +xs+, finally returning the list +[x | xs]+ (it's a list with +x+ as head and +xs+ as its tail).

[source, opa]
------------------------
parser x=rule xs={rep(n - 1, rule)} -> [x | xs]
------------------------

If +n = 0+ then we should succeed immediately and return empty list +[]+.

[source, opa]
------------------------
parser Rule.succeed -> []
------------------------

To succeed immediately we use the function from the standard library +Rule.succeed+ but in fact it's defined simply as:

[source, opa]
------------------------
succeed = parser "" -> void
------------------------

There are only two more PEG operators that we need to cover, the so-called _syntactic predicates_. The and-predicate +&A+ never consumes any inputs and succeeds only if +A+ succeeds on the given input. How is that useful? Well, it means that +A &B+ parses +A+ but *only if* it is followed by +B+. Similarly +&A B+ parses +B+ but only if it also matches +A+.

There is a dual not-predicate: +!A+ which succeeds only if +A+ *fails*. Therefore +A !B+ parses +A+ but only if it's not followed by +B+ and +!A B+ parses +B+ if it does not match +A+.

Let's see it at work.

[source, opa]
------------------------
eol = parser [\n]
full_line = parser line=((!eol .)*) eol -> "{line}"
------------------------

So +eol+ is simple parser that recognizes the end of the line. Then +full_line+ is a parser consuming a single line of text. How's that? Well +(!eol .)+ consumes any character that is not the end of the line. So +(!eol .)*+ consumes all the characters until reaching the end of the line. Finally we consume the end of the line itself.

In this post we discussed how to write parsers in Opa. There's more to say on the subject but the post is long enough as it is. I encourage you to consult the sources of the https://github.com/MLstate/opalang/blob/master/stdlib/core/parser/rules.opa[+Rule+ module] in the standard library, which defines a number of parsers and parser combinators (functions returning parsers) -- studying their implementations can be a great way to learn more. Also, if you want to practice then you can try the following exercises (or maybe you have some format for which you'd like to write a parser? Don't forget to let me know of the outcome!).

[icons=None, caption="Exercises"]
[NOTE]
=======================
Write parsers to recognize the following languages:

* http://en.wikipedia.org/wiki/Palindrome[palindroms] over +a+ and +b+ letters
* a^n^b^n^
* Small calculator accepting arithmetical expressions with basic operators (+, -, *, /) and giving the value of the expression as the parsing result. Bonus: correct operators precedences. Extension: add parentheses, more operators...
* a word composed of +a+ and +b+ letters with +b+ at the end (that's the example mentioned before in this post)
* a^n^b^n^c^n^
=======================

In the following post we will use what we learned today to write a parser for configuration files of iMage (as explained <<chapter_image_intro, before>>) -- stay tuned! And don't be shy to shout in the comments if there's something you like/don't like/don't understand/... Happy Opa-coding!
